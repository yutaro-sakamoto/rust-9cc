use std::str::FromStr;
use crate::ast::*;

grammar;
pub Program: Program = {
    <program_units:ProgramUnit*> => Program { program_units },
}

ProgramUnit: ProgramUnit = {
    <data_type: DataType> <func_name: Identifier> "(" <parameters: Parameters?> ")" <block: BlockStatement> => {
        match parameters {
            Some(parameters) => ProgramUnit::FuncDef(data_type, func_name, parameters, Box::new(block)),
            None => ProgramUnit::FuncDef(data_type, func_name, vec![], Box::new(block)),
        }
    },
    <statement: Statement> => ProgramUnit::Statement(Box::new(statement)),
}

Parameters: Vec<(DataType, String)> = {
    <parameter: Parameter> <rest: RestParameter*> => {
        let mut ret = vec![parameter];
        ret.extend(rest);
        ret
    }
}

Parameter: (DataType, String) = {
    <data_type: DataType> <identifier: Identifier> => (data_type, identifier),
}

RestParameter: (DataType, String) = {
    "," <Parameter> => <>,
}
        
Statement: Statement = {
    <AssignStatement> ";" => <>,
    <AssignPointerStatement> ";" => <>,
    <SingleExprStatement> ";" => <>,
    "return" <Expr> ";" => Statement::Return(Box::new(<>)),
    "if" "(" <condition: Expr> ")" <if_block: BlockStatement> <else_block: ElseBlock?> =>
        Statement::If(Box::new(condition), Box::new(if_block), Box::new(else_block)),
    <BlockStatement> => <>,
    "while" "(" <condition: Expr> ")" <block: BlockStatement> =>
        Statement::While(Box::new(condition), Box::new(block)),
    "for" "(" <init: ForInit?> ";" <condition: Expr?> ";" <step: ForStep?> ")" <block: BlockStatement> =>
        Statement::For(Box::new(init), Box::new(condition), Box::new(step), Box::new(block)),
    "break" ";" => Statement::Break,
    <data_type: DataType> <identifier: Identifier> ";" => Statement::VarDef(data_type, identifier),
}

DataType: DataType = {
    <base_type: BaseType> <pointers: Pointer+> =>
        DataType::Pointer(pointers.len() as u32, Box::new(DataType::Primitive(base_type))),
    <BaseType> => DataType::Primitive(<>),
}
Pointer: () = {
    "*" => (),
}

BaseType: PrimitiveType = {
    "int" => PrimitiveType::Int,
}

AssignStatement: Statement = {
    <identifier: Identifier> "=" <expr: Expr> => Statement::Assign(identifier, Box::new(expr)),
}

AssignPointerStatement: Statement = {
    "*" <identifier: Identifier> "=" <expr: Expr> => Statement::AssignPointer(identifier, Box::new(expr)),
}

SingleExprStatement: Statement = {
    <expr: Expr> => Statement::Expr(Box::new(expr)),
}

ForInit: Statement = {
    <AssignStatement> => <>,
    <SingleExprStatement> => <>,
}

ForStep: Statement = {
    <AssignStatement> => <>,
    <SingleExprStatement> => <>,
}

ElseBlock: Statement = {
    "else" <BlockStatement> => <>,
}

BlockStatement: Statement = {
    "{" <statements:Statement*> "}" => Statement::Block(statements),
}

Identifier: String = {
    r"[a-zA-Z_][a-zA-Z0-9_]*" => String::from_str(<>).unwrap(),
}

Expr: Expr = {
    <ArithExpr> => Expr::ArithExpr(Box::new(<>)),
    <left:ArithExpr> "==" <right:ArithExpr> => Expr::Equal(Box::new(left), Box::new(right)),
    <left:ArithExpr> "!=" <right:ArithExpr> => Expr::NotEqual(Box::new(left), Box::new(right)),
    <left:ArithExpr> "<" <right:ArithExpr> => Expr::Less(Box::new(left), Box::new(right)),
    <left:ArithExpr> "<=" <right:ArithExpr> => Expr::LessOrEqual(Box::new(left), Box::new(right)),
    <left:ArithExpr> ">" <right:ArithExpr> => Expr::Less(Box::new(right), Box::new(left)),
    <left:ArithExpr> ">=" <right:ArithExpr> => Expr::LessOrEqual(Box::new(right), Box::new(left)),
}

ArithExpr: ArithExpr = {
    <Factor> => ArithExpr::Factor(Box::new(<>)),
    <expr:ArithExpr> "+" <factor:Factor> => ArithExpr::Add(Box::new(expr), Box::new(factor)),
    <expr:ArithExpr> "-" <factor:Factor> => ArithExpr::Sub(Box::new(expr), Box::new(factor)),
}

Factor: Factor = {
    <Unary> => Factor::Unary(Box::new(<>)),
    <left:Factor> "*" <right:Unary> => Factor::Mul(Box::new(left), Box::new(right)),
    <left:Factor> "/" <right:Unary> => Factor::Div(Box::new(left), Box::new(right)),
}

Unary: Unary = {
    "-" <atom:Atom> => Unary::Neg(Box::new(atom)),
    "+" <atom:Atom> => Unary::Atom(Box::new(atom)),
    "*" <atom:Atom> => Unary::PointerDeref(Box::new(atom)),
    <atom:Atom> => Unary::Atom(Box::new(atom)),
}

Atom: Atom = {
    r"[0-9]+" => Atom::Number(i32::from_str(<>).unwrap()),
    "(" <expr:Expr> ")" => Atom::Expr(Box::new(expr)),
    "&" <Identifier> => Atom::AddressOf(<>),
    <identifier:Identifier> <arguments:ArgumentList?> => {
        match arguments {
            Some(arguments) => Atom::FunctionCall(identifier, arguments),
            None => Atom::Variable(identifier),
        }
    }
}

ArgumentList: Vec<Expr> = {
    "(" ")" => vec![],
    "(" <arg:Expr> <rest_args: RestArgument*> ")" => {
        let mut ret = vec![arg];
        ret.extend(rest_args);
        ret
    }
}

RestArgument: Expr = {
    "," <arg:Expr> => arg,
}